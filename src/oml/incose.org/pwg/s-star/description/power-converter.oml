description <http://incose.org/pwg/s-star/description/power-converter#> as power-converter {
			
	// Systems
	
	uses <http://www.w3.org/2000/01/rdf-schema#> as rdfs

	uses <http://incose.org/pwg/s-star/vocabulary/interface#> as interface

	uses <http://incose.org/pwg/s-star/vocabulary/power-interface#> as power-interface

	@rdfs:label "Local Power Distribution System"
	ci lpds : interface:System
	
	@rdfs:label "International Power Converter"
	ci ipc : interface:System
	
	@rdfs:label "Externally-Powered Device"
	ci epd : interface:System
	
	@rdfs:label "Device and Converter User"
	ci dcu : interface:System
	
	@rdfs:label "Local Environment"
	ci le : interface:System
	
	// Interfaces
	
	@rdfs:label "Local Power Output Interface"
	ci lpds_po : power-interface:ElectricalPowerOutputInterface
	
	@rdfs:label "International Power Converter Power Input Interface"
	ci ipc_pi : power-interface:ElectricalPowerInputInterface
	
	@rdfs:label "International Power Converter Power Output Interface"
	ci ipc_po : power-interface:ElectricalPowerOutputInterface
	
	@rdfs:label "Externally Powered Device Power Input Interface"
	ci epd_pi : power-interface:ElectricalPowerInputInterface
	
	// Input/Outputs
	
	@rdfs:label "Input Power"
	ci ip : power-interface:ElectricalPower
	
	@rdfs:label "Output Power"
	ci op : power-interface:ElectricalPower
	
	// Interactions
	
	@rdfs:label "Transfer Input Electrical Power"
	ci tip : power-interface:TransferElectricalPower
	
	@rdfs:label "Transfer Output Electrical Power"
	ci top : power-interface:TransferElectricalPower
	
	// Systems of Access
	
	@rdfs:label "Local Power to Converter SOA"
	ci lpds_ipc : power-interface:PowerDistributionSystemOfAccess
	
	@rdfs:label "Converter to Externally-Powered Device SOA"
	ci ipc_epd : power-interface:PowerDistributionSystemOfAccess
	
	// Ports
	
	@rdfs:label "PI.1"
	ci pi_1 : interface:Port
	
	@rdfs:label "PO.1"
	ci po_1 : interface:Port
	
	// Architectural Relationships
	
	@rdfs:label "AR.1"
	ci ar_1 : interface:ArchitecturalRelationship
	
	@rdfs:label "AR.2"
	ci ar_2 : interface:ArchitecturalRelationship
	
	// System relations
	
	ref ci lpds [
		interface:providesInterface lpds_po
		interface:interactsThrough pi_1
	]
	
	ref ci ipc [
		interface:providesInterface ipc_pi
		interface:providesInterface ipc_po
		interface:interactsThrough pi_1
		interface:interactsThrough po_1
	]
	
	ref ci epd [
		interface:providesInterface epd_pi
		interface:interactsThrough po_1
	]
	
	// Interface relations
	
	ref ci lpds_po [
		interface:permitsInputOutput ip
		interface:permitsSOA lpds_ipc
		interface:permitsFunctionalInteraction tip
		interface:groups pi_1
	]
	
	ref ci ipc_pi [
		interface:permitsInputOutput ip
		interface:permitsSOA lpds_ipc
		interface:permitsFunctionalInteraction tip
		interface:groups pi_1
		interface:permitsArchitecturalRelationship ar_1
	]
	
	ref ci ipc_po [
		interface:permitsInputOutput op
		interface:permitsSOA ipc_epd
		interface:permitsFunctionalInteraction top
		interface:groups po_1
		interface:permitsArchitecturalRelationship ar_2
	]
	
	ref ci epd_pi [
		interface:permitsInputOutput op
		interface:permitsSOA ipc_epd
		interface:permitsFunctionalInteraction top
		interface:groups po_1
	]
	
	// Input/output relations
	
	// Interaction relations
	
	// Port relations
	
	ref ci pi_1 [
		interface:receives ip
		interface:isUsedDuring tip
		interface:isFacilitatedByInternally lpds_ipc
	]
	
	ref ci po_1 [
		interface:sends op
		interface:isUsedDuring top
		interface:isFacilitatedByInternally ipc_epd
	]
	
	// ArchitecturalRelationship relations
	
	ref ci ar_1 [
		interface:emergesFrom ip
	]
	
	ref ci ar_2 [
		interface:emergesFrom op
	]
}
